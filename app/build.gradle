apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion android_build.compile_sdk
  buildToolsVersion android_build.build_tools

  defaultConfig {
    applicationId "com.github.mbarrben.moviedb"
    minSdkVersion android_build.min_sdk
    targetSdkVersion android_build.target_sdk
    versionCode 1
    versionName "1.0"
    vectorDrawables.useSupportLibrary = true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    Properties privateProperties = DefaultProperties.create()

    buildConfigField "String", "API_KEY", "\"${privateProperties['API_KEY']}\""
    buildConfigField "String", "API_READ_ACCESS_TOKEN", "\"${privateProperties['API_READ_ACCESS_TOKEN']}\""
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }

  dexOptions {
    preDexLibraries true
    javaMaxHeapSize '4G'
  }
}

kapt {
  generateStubs = true
}

dependencies {
  compile project(':common')
  compile project(':domain')

  compile libs.support.appcompat
  compile libs.support.cardview
  compile libs.support.recyclerview

  compile libs.rxbindings.recyclerview

  compile libs.picasso
  compile libs.picasso_downloader
  compile libs.timber
  compile libs.byteunits

  compile libs.anko
  compile libs.anko_support
  compile libs.rx_android
  compile libs.rx_kotlin

  kapt libs.dagger_compiler
  compile libs.dagger
  provided libs.jsr250

  testCompile 'junit:junit:4.12'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'javax.inject', module: 'javax.inject'
    exclude group: 'com.squareup', module: 'javawriter'
  }
  androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.android.support', module: 'recyclerview-v7'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'appcompat-v7'
    exclude group: 'com.android.support', module: 'design'
  }
  androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  kaptAndroidTest libs.dagger_compiler
  androidTestCompile 'com.github.fabioCollini:DaggerMock:0.6.2'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.2') {
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }
  androidTestCompile('com.android.support.test:runner:0.5') {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestCompile('com.android.support.test:rules:0.5') {
    exclude group: 'com.android.support', module: 'support-annotations'
  }

  androidTestCompile 'org.mockito:mockito-core:2.3.6'
  androidTestCompile('com.nhaarman:mockito-kotlin:1.0.1') {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-runtime'
  }
  androidTestCompile('org.amshove.kluent:kluent:1.11') {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-runtime'
  }
}
